apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.openrct2"
        minSdkVersion 16
        targetSdkVersion 25

        versionCode 2
        versionName "0.0.6"

        externalNativeBuild {
            cmake {
                arguments '-DDEBUG_LEVEL=3', '-DANDROID_STL=c++_shared', '-DANDROID_CPP_FEATURES=rtti exceptions' //, '-DCMAKE_BUILD_TYPE=RELWITHDEBINFO'

                //abiFilters 'x86', 'x86_64'
                // abiFilters 'armeabi-v7a', 'arm64-v8a'
                abiFilters 'armeabi-v7a'
                targets 'openrct2'
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file("external/debug.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-DEBUG"
        }
        develop {
            applicationIdSuffix '.develop'
            versionNameSuffix "-DEVELOP"
        }
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
}

apply plugin: 'de.undercouch.download'

android.applicationVariants.all { variant ->
    variant.mergeAssets.doLast {
        copy {
            from '../../../data'
            into "$variant.mergeAssets.outputDir/data"
        }
        download {
            src 'https://github.com/marijnvdwerf/openrct2-dependencies-android/releases/download/v0.7/g2.dat'
            dest "$variant.mergeAssets.outputDir/data"
        }
    }
}

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.android.gms:play-services-analytics:10.2.1'
}

apply plugin: 'com.google.gms.google-services'
